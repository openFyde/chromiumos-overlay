# Copyright 2022 The ChromiumOS Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

CROS_WORKON_PROJECT="chromiumos/third_party/rust_crates"
CROS_WORKON_EGIT_BRANCH="main"
CROS_WORKON_LOCALNAME="rust_crates"
CROS_WORKON_OUTOFTREE_BUILD=1

PYTHON_COMPAT=( python3_{6..9} )

inherit cros-workon cros-rust python-single-r1

DESCRIPTION="Sources of third-party crates used by ChromeOS"
HOMEPAGE='https://chromium.googlesource.com/chromiumos/third_party/rust_crates/+/HEAD/'
KEYWORDS="~*"

# There's no obvious need for testing these crates at the moment. Further,
# testing these crates could be complicated, since we're pulling in the union of
# all crates needed for all CrOS projects on all architectures. Future Work(TM).
RESTRICT="test"

EXPECTED_LICENSES=(
	0BSD
	Apache-2.0
	BSD
	ISC
	MIT
	MPL-2.0
	ZLIB
)

LICENSE="${EXPECTED_LICENSES[*]}"

# A list of crate versions which we've fully replaced.
# FIXME(b/240953811): Remove this when our migration is done.
RDEPEND="
	!=dev-rust/memchr-2.4.0
	!=dev-rust/regex-1.5.4
	!=dev-rust/termcolor-1.1.2
"

# A list of crate versions available in rust_crates, which we can install in
# dev-rust. Logically, this is "all crates, minus those in a blocklist," but we
# have to have this info in `pkg_*` functions, so inspecting `rust_crates`
# sources and our blocklist isn't possible.
#
# FIXME(b/240953811): Remove this when our migration is done.
ALLOWED_CRATE_VERSIONS=(
	# NOTE: This list was generated by
	# ${FILESDIR}/write_allowlisted_crate_versions.py. Any
	# modifications may be overwritten.
	"addr2line-0.14.1"
	"adler-0.2.3"
	"android_system_properties-0.1.5"
	"anyhow-1.0.62"
	"argh-0.1.8"
	"argh_derive-0.1.8"
	"argh_shared-0.1.8"
	"async-task-4.3.0"
	"async-trait-0.1.48"
	"backtrace-0.3.56"
	"bitvec-0.19.5"
	"bumpalo-3.11.0"
	"bytes-1.2.1"
	"cc-1.0.73"
	"cexpr-0.5.0"
	"chunked_transfer-1.4.0"
	"clipboard-win-4.2.1"
	"cmake-0.1.45"
	"com_logger-0.1.1"
	"configparser-3.0.0"
	"core-foundation-sys-0.8.3"
	"crc32fast-1.3.2"
	"crossbeam-channel-0.5.6"
	"crossbeam-utils-0.8.11"
	"cxx-1.0.42"
	"cxxbridge-flags-1.0.42"
	"cxxbridge-macro-1.0.42"
	"dbus-0.6.5"
	"dbus-0.9.5"
	"dbus-crossroads-0.4.0"
	"dbus-crossroads-0.5.1"
	"dbus-tokio-0.7.3"
	"dbus-tree-0.9.2"
	"derive-into-owned-0.1.0"
	"enumn-0.1.5"
	"errno-0.2.8"
	"errno-dragonfly-0.1.2"
	"error-code-2.3.0"
	"failure-0.1.8"
	"failure_derive-0.1.8"
	"fd-lock-2.0.0"
	"fd-lock-3.0.6"
	"funty-1.1.0"
	"futures-0.3.14"
	"futures-channel-0.3.14"
	"futures-core-0.3.14"
	"futures-executor-0.3.14"
	"futures-io-0.3.14"
	"futures-macro-0.3.14"
	"futures-sink-0.3.14"
	"futures-task-0.3.14"
	"futures-util-0.3.14"
	"getrandom-0.1.16"
	"getrandom-0.2.2"
	"gimli-0.23.0"
	"grpcio-compiler-0.6.0"
	"hashbrown-0.12.3"
	"hermit-abi-0.1.18"
	"httparse-1.7.1"
	"iana-time-zone-0.1.47"
	"indexmap-1.9.1"
	"inotify-0.9.3"
	"inotify-sys-0.1.5"
	"intrusive-collections-0.9.4"
	"io-lifetimes-0.7.3"
	"io-uring-0.5.4"
	"itoa-1.0.3"
	"js-sys-0.3.59"
	"libc-0.2.132"
	"libdbus-sys-0.2.2"
	"link-cplusplus-1.0.5"
	"linux-raw-sys-0.0.46"
	"lock_api-0.4.2"
	"matches-0.1.9"
	"memchr-2.4.0"
	"miniz_oxide-0.4.3"
	"miow-0.3.6"
	"nix-0.19.1"
	"nix-0.20.0"
	"nix-0.23.1"
	"nom-6.1.2"
	"nom-7.1.1"
	"ntapi-0.3.6"
	"num-0.1.42"
	"num-bigint-0.4.3"
	"num-integer-0.1.45"
	"num-iter-0.1.43"
	"num-traits-0.2.14"
	"num_enum-0.5.7"
	"num_enum_derive-0.5.7"
	"num_threads-0.1.6"
	"object-0.23.0"
	"openssl-0.10.41"
	"paste-1.0.4"
	"proc-macro-crate-1.2.1"
	"proc-macro-nested-0.1.7"
	"protobuf-2.27.1"
	"protoc-2.27.1"
	"protoc-grpcio-2.0.0"
	"protoc-rust-2.27.1"
	"quote-0.3.15"
	"radium-0.5.3"
	"rand_chacha-0.3.0"
	"rand_core-0.6.1"
	"rand_hc-0.3.0"
	"redox_syscall-0.2.4"
	"redox_users-0.4.0"
	"regex-1.5.4"
	"remain-0.2.4"
	"remove_dir_all-0.5.3"
	"rustc-demangle-0.1.18"
	"rustix-0.35.9"
	"rustversion-1.0.9"
	"rustyline-8.2.0"
	"rustyline-9.1.2"
	"rustyline-derive-0.4.0"
	"serde-1.0.126"
	"serde_derive-1.0.126"
	"shell-words-1.1.0"
	"shlex-1.0.0"
	"slab-0.4.2"
	"socket2-0.3.19"
	"str-buf-1.0.5"
	"synom-0.11.3"
	"tap-1.0.1"
	"termcolor-1.1.2"
	"thiserror-1.0.32"
	"thiserror-impl-1.0.32"
	"time-0.1.43"
	"time-0.3.14"
	"tinyvec-1.6.0"
	"tokio-macros-1.1.0"
	"tokio-stream-0.1.3"
	"toml-0.5.9"
	"uart_16550-0.2.18"
	"unicode-bidi-0.3.8"
	"unicode-normalization-0.1.21"
	"unicode-segmentation-1.7.1"
	"unicode-xid-0.0.4"
	"unicode-xid-0.2.1"
	"volatile-0.4.5"
	"walkdir-2.3.1"
	"wasi-0.10.2+wasi-snapshot-preview1"
	"wasi-0.9.0+wasi-snapshot-preview1"
	"wasm-bindgen-0.2.82"
	"wasm-bindgen-backend-0.2.82"
	"wasm-bindgen-macro-0.2.82"
	"wasm-bindgen-macro-support-0.2.82"
	"wasm-bindgen-shared-0.2.82"
	"winapi-i686-pc-windows-gnu-0.4.0"
	"winapi-util-0.1.5"
	"winapi-x86_64-pc-windows-gnu-0.4.0"
	"windows-sys-0.36.1"
	"windows_aarch64_msvc-0.36.1"
	"windows_i686_gnu-0.36.1"
	"windows_i686_msvc-0.36.1"
	"windows_x86_64_gnu-0.36.1"
	"windows_x86_64_msvc-0.36.1"
	"wyz-0.2.0"
	"x86_64-0.14.10"
	"zeroize-1.5.7"
	"zeroize_derive-1.3.2"
)

src_unpack() {
	# Do this first so "${S}" is set up as early as possible. This also
	# prevents cros-rust_src_unpack from modifying ${S}.
	cros-workon_src_unpack
	cros-rust_src_unpack
}

src_prepare() {
	[[ -n "${PATCHES[*]}" ]] && die "User patches are not supported in" \
		"this ebuild. Instead, add patches to" \
		"third_party/rust_crates; please see the README for details."
	# Call eapply_user; otherwise, portage gets upset.
	eapply_user
}

src_configure() {
	:
}

src_compile() {
	# For lack of a better place to put this (since we want it to run when
	# FEATURES=test is not enabled), verify licenses here.
	"${S}/verify_licenses.py" \
		--license-file="${S}/licenses_used.txt" \
		--expected-licenses="${EXPECTED_LICENSES[*]}" \
		|| die
	einfo "License verification complete."

	# If we're working on out-of-tree sources, mirror licenses to make
	# license checks happy. This is a bit hacky, but cheap.
	if [[ "${S}" != "${WORKDIR}"/* ]]; then
		local targ="${WORKDIR}/licenses"
		[[ -e "${targ}" ]] && die "${targ} shouldn't exist"
		einfo "Mirroring licenses from ${S}/vendor to ${targ}..."
		rsync -r --include='*/' --include='LICENSE*' --exclude='*' \
			--prune-empty-dirs "${S}/vendor" "${targ}" || die
	fi
}

# Shellcheck thinks CROS_RUST variables are never defined.
# shellcheck disable=SC2154
src_install() {
	insinto "${CROS_RUST_REGISTRY_DIR}"
	# Prebuilt .a files are installed by some packages, and should not be
	# stripped.
	dostrip -x "${CROS_RUST_REGISTRY_DIR}"
	cd "${S}/vendor" || die
	doins -r "${ALLOWED_CRATE_VERSIONS[@]}"
}

pkg_preinst() {
	cros-rust_cleanup_vendor_registry_links "${ALLOWED_CRATE_VERSIONS[@]}"
}

pkg_postinst() {
	cros-rust_create_vendor_registry_links \
		"${S}/vendor" \
		"${ALLOWED_CRATE_VERSIONS[@]}"
}

pkg_prerm() {
	cros-rust_cleanup_vendor_registry_links "${ALLOWED_CRATE_VERSIONS[@]}"
}

pkg_postrm() {
	cros-rust_create_vendor_registry_links \
		"${S}/vendor" \
		"${ALLOWED_CRATE_VERSIONS[@]}"
}
